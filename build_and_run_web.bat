@echo off
:: Requires `cargo` and some modern version of `rustc`
:: make sure to get the files are named according to the project name

:: SETUP
WHERE wasm-bindgen >NUL 2>NUL
IF %ERRORLEVEL% NEQ 0 (
    cargo install -f wasm-bindgen-cli
)

WHERE simple-http-server >NUL 2>NUL
IF %ERRORLEVEL% NEQ 0 (
    cargo install simple-http-server
)

:: BUILD
cargo build --release --target wasm32-unknown-unknown
if %ERRORLEVEL% NEQ 0 GOTO :EOF

wasm-bindgen --out-dir ".\target\web-build" --target web ".\target\wasm32-unknown-unknown\release\pirate-jam-15.wasm
if %ERRORLEVEL% NEQ 0 GOTO :EOF

:: RUN WEB
if not exist ".\target\web-export" mkdir ".\target\web-export"
if not exist ".\target\web-export\assets" mkdir ".\target\web-export\assets"
xcopy ".\assets" ".\target\web-export\assets" /S /Y

:: the `snippets` folder is generated by `bevy_web_file_drop` to suport file drag&drop
if not exist ".\target\web-export\snippets" mkdir ".\target\web-export\snippets"
xcopy ".\target\web-build\snippets" ".\target\web-export\snippets" /S /Y

xcopy ".\static\index.html" ".\target\web-export" /Y
xcopy ".\target\web-build\pirate-jam-15.js" ".\target\web-export" /Y
xcopy ".\target\web-build\pirate-jam-15_bg.wasm" ".\target\web-export" /Y

simple-http-server ".\target\web-export"
